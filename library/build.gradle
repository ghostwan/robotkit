apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: "maven"

group = POM_GROUP_ID
version = VERSION_NAME

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    api 'com.aldebaran:qisdk:1.4.8'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

dokka {
    outputFormat = 'html'
    outputDirectory = "../docs/javadoc"
    skipEmptyPackages = true
    externalDocumentationLink {
        url = new URL("https://android.aldebaran.com/sdk/doc/libqi-java/")
    }
    externalDocumentationLink {
        url = new URL("https://android.aldebaran.com/sdk/doc/qisdk/")
    }
}

task javadok(type: Jar, dependsOn: dokka) {
    from "../docs/javadoc"
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadok
}

//uploadArchives {
//    repositories {
//        mavenDeployer {
//            repository(url: 'https://api.bintray.com/maven/'+USER_NAME+'/'+REPOSITORY_NAME+'/'+POM_ARTIFACT_ID) {
//                authentication(
//                        userName: System.getenv('BINTRAY_USER'),
//                        password: System.getenv('BINTRAY_API_KEY')
//                )
//            }
//            pom.artifactId = POM_ARTIFACT_ID
//            pom.groupId = POM_GROUP_ID
//            pom.version = VERSION_NAME
//        }
//    }
//}


